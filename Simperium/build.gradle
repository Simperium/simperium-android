
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies { classpath 'com.android.tools.build:gradle:1.0.0' }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

version gitVersion()

group "com.simperium"

repositories {
    mavenCentral()
    maven { url "http://simperium.github.io/simperium-android" }
}

android {
  
    defaultPublishConfig 'clientRelease'

    dependencies {
        compile 'com.koushikdutta.async:androidasync:2.+@jar'
        compile 'org.thoughtcrime.ssl.pinning:AndroidPinning:1.0.0'
    }

    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 21
        versionName project.version

        // Calculating PIN for certificate: OU=Domain Control Validated, CN=*.simperium.com
        // Pin Value: e25695097d04927c9d90206333a687a7b1f99708
        buildConfigField 'String', 'SIMPERIUM_COM_SPKI', '"e25695097d04927c9d90206333a687a7b1f99708"'
    }

    productFlavors {
        // client is the main production build
        client
        // support build is for running tests and includes mock code
        support
    }

}

task versionConfig(group: "build", description: "Generate version class") {
    def dir = "${buildDir}/generated/source/version"
    def file = new File(dir, "com/simperium/Version.java")

    android.sourceSets.main.java.srcDir dir

    doLast {
        def describe = gitDescribe()

        logger.info "Version: ${describe}"

        file.getParentFile().mkdirs()
        def writer = new FileWriter(file)
        writer.write("""/* auto-generated file do not modify */
package com.simperium;

public final class Version {

    public static final String LIBRARY_NAME = "android";

    public static final String NAME = "android-${project.version}";

    /* project.version */
    public static final String NUMBER = "${project.version}";

    /* git rev-parse --short --verify HEAD */
    public static final String BUILD = "${gitHash()}";

    /* git describe --always --dirty=-dirty */
    public static final String DESCRIBE = "${gitDescribe()}";
}
""")
        writer.close()
    }
}

tasks.preBuild.dependsOn versionConfig

afterEvaluate {

    configurations {
        archives {
            extendsFrom configurations.default
        }
    }

    signing {
        sign configurations.archives
    }

    uploadArchives {
        configuration = configurations.archives
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                if (project.hasProperty("ossrhUsername") && project.hasProperty("ossrhPassword")) {
                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                      authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                      authentication(userName: ossrhUsername, password: ossrhPassword)
                    }
                }


                pom.project {
                    name "Simperium Android Library"

                    groupId 'com.simperium.android'
                    artifactId 'simperium'

                    packaging 'aar'
                    description 'Android Library for building applications using Simperium'

                    url 'https://github.com/simperium/simperium-android'
                    scm {
                        connection 'scm:git:https://github.com/simperium/simperium-android.git'
                        developerConnection 'scm:git:https://github.com/simperium/simperium-android.git'
                        url 'https://github.com/simperium/simperium-android.git'
                    }

                    licenses {
                        license {
                            name 'The MIT License (MIT)'
                            url 'http://opensource.org/licenses/MIT'
                        }
                    }

                    developers {

                        developer {
                            id 'beaucollins'
                            name 'Robert Collins'
                            email 'robert@automattic.com'
                        }

                    }
                }
            }
        }
    }
}
