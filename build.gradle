// Top-level build file where you can add configuration options common to all sub-projects/modules.
def gitHash() {
    try {
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--verify', '--short', 'HEAD'
            standardOutput = code
        }
        return code.toString().trim()
    }
    catch (ignored) {
        return "<unknown>";
    }
}

def gitDescribe() {
    try {
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--always', '--dirty=-dirty'
            standardOutput = code
        }
        return code.toString().trim()
    }
    catch (ignored) {
        return "<unknown>"
    }
}

def static gitVersion() {
    '0.10.1'
}

allprojects {
    tasks.withType(Test).configureEach {
        maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
        if (!project.hasProperty("createReports")) {
            reports.html.required = false
            reports.junitXml.required = false
        }
        forkEvery = 100
    }
}
